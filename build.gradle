import nextflow.plugins.JsonPluginTask
import nextflow.plugins.ZipPluginTask

plugins {
    id "java"
    id "groovy"
    id "idea"
}

repositories {
    mavenCentral()
}

configurations {
    // see https://docs.gradle.org/4.1/userguide/dependency_management.html#sub:exclude_transitive_dependencies
    runtimeClasspath.exclude group: 'org.slf4j', module: 'slf4j-api'
}

ext{
    nextflowVersion = '23.04.0'
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    compileOnly "io.nextflow:nextflow:$nextflowVersion"
    compileOnly 'org.slf4j:slf4j-api:1.7.10'
    compileOnly 'org.pf4j:pf4j:3.4.1'

    // add here plugins dependencies
    implementation 'org.apache.poi:poi:5.2.4'
    implementation 'org.apache.poi:poi-ooxml:5.2.4'

    // test plugin
    testImplementation "io.nextflow:nextflow:$nextflowVersion"
    testImplementation ("org.spockframework:spock-core:2.2-groovy-3.0") { exclude group: 'org.codehaus.groovy'; exclude group: 'net.bytebuddy' }
    testImplementation ('org.spockframework:spock-junit4:2.2-groovy-3.0') { exclude group: 'org.codehaus.groovy'; exclude group: 'net.bytebuddy' }

    testImplementation(testFixtures("io.nextflow:nextflow:$nextflowVersion"))
    testImplementation(testFixtures("io.nextflow:nf-commons:$nextflowVersion"))

}

// use JUnit 5 platform
test {
    useJUnitPlatform()
}

version = '0.0.1'

// package
jar {
    manifest {
        attributes(
                'Manifest-Version':'1.0',
                'Plugin-Id':'nf-apache-poi',
                'Plugin-Version': archiveVersion,
                'Plugin-Class': 'es.puravida.poi.ApachePOIPlugin',
                'Plugin-Provider': 'nextflow',
                'Plugin-Requires': '>=22.10.0',
        )
    }
}

ext{
    github_organization = 'jagedn'
}

tasks.register('zipPlugin', ZipPluginTask){
}

tasks.register('jsonPlugin', JsonPluginTask){
    donwloadUrl = "https://github.com/${github_organization}/${project.name}/releases/download"
}
